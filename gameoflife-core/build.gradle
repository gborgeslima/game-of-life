apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: "findbugs"
apply plugin: "pmd"
apply plugin: "jacoco"

repositories {
    jcenter()
}

dependencies {
    testCompile(
            [group: 'junit', name: 'junit', version: '4.8.1'],
            [group: 'org.hamcrest', name: 'hamcrest-all', version: '1.1']
    )
}

findbugs {
  ignoreFailures = true
  toolVersion = "3.0.0"
  sourceSets = [sourceSets.main]
  reportsDir = file("$project.buildDir/reports/findbugs")
  effort = "max"
}

pmd {
  ignoreFailures = true
  sourceSets = [sourceSets.main]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSets = [ "java-basic", "java-strings", "java-braces" ]
}

jacoco {
    reportsDir = file("$project.buildDir/reports/jacoco")
}

// add CPD to check
check << {
  File outDir = new File('build/reports/pmd/')
  outDir.mkdirs()
  ant.taskdef(name: 'cpd', classname: 'net.sourceforge.pmd.cpd.CPDTask',
    classpath: configurations.pmd.asPath)
  ant.cpd(minimumTokenCount: '100', format: 'xml',
    outputFile: new File(outDir , 'cpd.xml')) {
    fileset(dir: "src/main/java") {
      include(name: '**/*.java')
    }
  }
}

